import org.jetbrains.kotlin.konan.target.HostManager

buildscript {
    /*
     * These property group is used to build kotlinx-io against Kotlin compiler snapshot.
     * How does it work:
     * When build_snapshot_train is set to true, kotlin_version property is overridden with kotlin_snapshot_version,
     * atomicfu_version and coroutines_version is overwritten by TeamCity environment
     * (AFU and kx is built with snapshot and published to mavenLocal as previous step or the snapshot build).
     * DO NOT change the name of these properties without adapting kotlinx.train build chain.
     */
    def prop = rootProject.properties['build_snapshot_train']
    ext.build_snapshot_train = prop != null && prop != ""
    if (build_snapshot_train) {
        ext.kotlin_version = rootProject.properties['kotlin_snapshot_version']
        if (kotlin_version == null) {
            throw new IllegalArgumentException("'kotlin_snapshot_version' should be defined when building with snapshot compiler")
        }
        repositories {
            mavenLocal()
            maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        }
    }
 
    repositories {
        mavenCentral()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url 'https://dl.bintray.com/jetbrains/kotlin-native-dependencies' }
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()

        if (build_snapshot_train) {
            maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
            mavenLocal()
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.37"
        classpath group: 'net.rubygrapefruit', name: 'native-platform', version: '0.10'
        classpath "org.jetbrains.kotlinx:atomicfu-gradle-plugin:$atomicfu_version"
        classpath "com.moowork.gradle:gradle-node-plugin:$gradle_node_version"
    }
}

def skipPublish = ['benchmarks', 'binary-compatibility-validator']

apply from: "gradle/experimental.gradle"
apply from: rootProject.file('gradle/verifier.gradle')

allprojects {
    project.ext.hostManager = new HostManager()

    if (project.hasProperty('releaseVersion')) {
        project.version = project.releaseVersion
    }
    def DeployVersion = properties['DeployVersion']
    if (DeployVersion != null) {
        project.version = DeployVersion
    }

    if (build_snapshot_train) {
        def deployVersion = properties['DeployVersion']
        if (deployVersion != null) version = deployVersion
        ext.kotlin_version = rootProject.properties['kotlin_snapshot_version']
        println "Using Kotlin $kotlin_version for project $it"

        def skipSnapshotChecks = rootProject.properties['skip_snapshot_checks'] != null
        if (!skipSnapshotChecks && version != atomicfu_version) {
            throw new IllegalStateException("Current deploy version is $version, but atomicfu version is not overridden ($atomicfu_version) for $it")
        }

        if (!skipSnapshotChecks && version != coroutines_version) {
            throw new IllegalStateException("Current deploy version is $version, but coroutines version is not overridden ($coroutines_version) for $it")
        }

        kotlin_version = rootProject.properties['kotlin_snapshot_version']
        repositories {
            mavenLocal()
            maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        }
    }

    repositories {
        maven {
            url "https://kotlin.bintray.com/kotlinx"
            credentials {
                username = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER') ?: ""
                password = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY') ?: ""
            }
        }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
        maven { url "https://teamcity.jetbrains.com/guestAuth/app/rest/builds/id:1907319/artifacts/content/maven" }
        jcenter()
    }

    apply plugin: 'kotlin-multiplatform'
    apply plugin: 'kotlinx-atomicfu'

    apply from: rootProject.file("gradle/utility.gradle")

    apply from: rootProject.file("gradle/jvm.gradle")

    if (!skipPublish.contains(project.name)) {
        apply from: rootProject.file("gradle/common.gradle")
        apply from: rootProject.file("gradle/js.gradle")
        apply from: rootProject.file("gradle/native.gradle")
        apply from: rootProject.file("gradle/ide.gradle")

        apply from: rootProject.file("gradle/publish.gradle")

    }

    kotlin {
        configure(sourceSets) {
            def srcDir = name.endsWith('Main') ? 'src' : 'test'
            def platform = name[0..-5]

            kotlin.srcDir "$platform/$srcDir"

            if (!skipPublish.contains(project.name)) {
                languageSettings {
                    progressiveMode = true
                    experimentalAnnotations.each { useExperimentalAnnotation(it) }
                }
            }
        }

        configure(targets) {
            def targetName = it.name
            compilations.all { compilation ->
                def compileTask = tasks.getByName(compilation.compileKotlinTaskName)
                compileTask.kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=" + experimentalAnnotations.join(",")]
                if (targetName.contains("jvm") && compilation.compilationName == "main") {
                    compileTask.kotlinOptions.freeCompilerArgs += ["-Xdump-declarations-to=${buildDir}/visibilities.json"]
                }
            }

            def mainInterop = new File(project.projectDir, 'native/interop').listFiles()?.findAll {
                it.name.endsWith('.def')
            }
            def testInterop = new File(project.projectDir, 'native/interopTest').listFiles()?.findAll {
                it.name.endsWith('.def')
            }

            if (mainInterop && compilations.main.hasProperty('cinterops')) {
                compilations.main.cinterops { c ->
                    mainInterop.each { file ->
                        def filePath = file.absolutePath
                        def name = file.name[0..-5]

                        configure(c.create("${name}")) {
                            defFile filePath
                        }
                    }
                }
            }

            if (testInterop && compilations.hasProperty('test') && compilations.test.hasProperty('cinterops')) {
                compilations.test.cinterops { c ->
                    testInterop.each { file ->
                        def filePath = file.absolutePath
                        def name = file.name[0..-5]

                        configure(c.create("${name}")) {
                            defFile filePath
                        }
                    }
                }
            }
        }
    }
}

println("Using Kotlin compiler version: $org.jetbrains.kotlin.config.KotlinCompilerVersion.VERSION")
if (build_snapshot_train) {
    gradle.taskGraph.whenReady { // Hack to prevent kotlinCompilerClasspath + apiCheck interference
        println "Manifest of kotlin-compiler-embeddable.jar for kotlinx-io"
        configure(subprojects.findAll { it.name == "kotlinx-io" }) {
            configurations.matching { it.name == "kotlinCompilerClasspath" }.all {
                resolvedConfiguration.getFiles().findAll { it.name.contains("kotlin-compiler-embeddable") }.each {
                    def manifest = zipTree(it).matching {
                        include 'META-INF/MANIFEST.MF'
                    }.getFiles().first()

                    manifest.readLines().each {
                        println it
                    }
                }
            }
        }
    }

    afterEvaluate {
        kotlinNpmResolve.enabled = false
    }
}
